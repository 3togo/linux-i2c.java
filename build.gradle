plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}




def globalName    = 'linux-i2c'

def globalVersion = '0.0.0-alpha3'

def globalDesc    = 'Use the standard Linux I2C api from Java'

def globalUrl     = 'https://github.com/dvlopt/linux-i2c.java'




group   = 'io.dvlopt'
version = globalVersion




repositories {
    mavenCentral()
}


dependencies {
    api            'io.dvlopt:linux-common:0.1.0'
    implementation 'io.dvlopt:linux-io:0.0.3'
    testCompile    'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testRuntime    'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}




test {

    useJUnitPlatform()

    testLogging {

        events 'passed'  ,
               'skipped' ,
               'failed'
    }
}




task sourcesJar( type:      Jar     ,
                 dependsOn: classes ) {

    classifier = 'sources'
    from sourceSets.main.allSource
}




task javadocJar( type:      Jar     ,
                 dependsOn: javadoc ) {

    classifier = 'javadoc'
    from javadoc.destinationDir
}




def pomConfig = {


    licenses {
        license {
            name 'Apache License, Version 2.0'
            url  'http://www.apache.org/licenses/LICENSE-2.0'
        }
    }


    developers {
        developer {
            name            'Adam Helinski'
            email           'adam@dvlopt.io'
            organization    'Dvlopt'
            organizationUrl 'http://dvlopt.io'
        }
    }


    scm {
        connection          'scm:git:git://github.com/dvlopt/linux-i2c.java.git'
        developerConnection 'scm:git:ssh://github.com/dvlopt/linux-i2c.java.git'
        url                 globalUrl
    }
}




publishing {
    publications {
        mavenJava( MavenPublication ) {

            groupId    group
            artifactId 'linux-i2c'
            version    globalVersion

            from components.java

            artifact sourcesJar
            artifact javadocJar
            
            pom.withXml {
                def root = asNode()

                root.appendNode( 'name'     ,
                                 globalName )

                root.appendNode( 'description' ,
                                 globalDesc    )

                root.appendNode( 'url'     ,
                                 globalUrl )

                root.children().last() + pomConfig
            }
        }
    }
}




bintray {
    user = project.hasProperty( 'bintrayUser' ) ? project.property( 'bintrayUser' )
                                                : System.getenv( 'BINTRAY_USER' )
    key  = project.hasProperty( 'bintrayKey' ) ? project.property( 'bintrayKey' ) 
                                               : System.getenv( 'BINTRAY_KEY' )

    publications = [ 'mavenJava' ]

    pkg {
        repo                  = 'maven'
        name                  = globalName
        desc                  = globalDesc
        licenses              = [ 'Apache-2.0' ]
        websiteUrl            = globalUrl
        vcsUrl                = 'https://github.com/dvlopt/linux-i2c.java.git'
        labels                = [ 'java', 'jna', 'native', 'linux', 'i2c', 'standard' ]
        publicDownloadNumbers = true
        
        version {
            name   = globalVersion
            vcsTag = globalVersion

            gpg {
                sign = true
            }
        }
    }
}




javadoc {
    options.links( 'https://docs.oracle.com/javase/8/docs/api/'      ,
                   'https://dvlopt.github.io/doc/java/linux-common/' )
}
